<h2>LAB: more timing strategies, finite state machines</h2>
<br/>As a quick review, here is the code we were looking at last week (demonstrating a timer in arduino):
<pre class="lang:arduino decode:true " >
    /* Blink without Delay

 Turns on and off a light emitting diode(LED) connected to a digital
 pin, without using the delay() function.  This means that other code
 can run at the same time without being interrupted by the LED code.

 The circuit:
 * LED attached from pin 13 to ground.
 * Note: on most Arduinos, there is already an LED on the board
 that's attached to pin 13, so no hardware is needed for this example.


 created 2005
 by David A. Mellis
 modified 8 Feb 2010
 by Paul Stoffregen

 This example code is in the public domain.


 http://www.arduino.cc/en/Tutorial/BlinkWithoutDelay
 */

// constants won't change. Used here to
// set pin numbers:
const int ledPin =  13;      // the number of the LED pin

// Variables will change:
int ledState = LOW;             // ledState used to set the LED
long previousMillis = 0;        // will store last time LED was updated

// the follow variables is a long because the time, measured in miliseconds,
// will quickly become a bigger number than can be stored in an int.
long interval = 1000;           // interval at which to blink (milliseconds)

void setup() {
  // set the digital pin as output:
  pinMode(ledPin, OUTPUT);
}

void loop()
{

  // check to see if it's time to blink the LED; that is, if the
  // difference between the current time and last time you blinked
  // the LED is bigger than the interval at which you want to
  // blink the LED.
  unsigned long currentMillis = millis();

  if(currentMillis - previousMillis > interval) {
    // save the last time you blinked the LED
    previousMillis = currentMillis;

    // if the LED is off turn it on and vice-versa:
    if (ledState == LOW)
      ledState = HIGH;
    else
      ledState = LOW;

    // set the LED with the ledState of the variable:
    digitalWrite(ledPin, ledState);
  }
  delay(3);
}
</pre>
<br/>So this blinks one LED, which is fine, but what if we want to blink two LEDs via timer?
<br/>In order to do this we are going to write a blink function. There are 2 functions that we talk about all the time in Arduino:  <span class="lang:arduino decode:true  crayon-inline " >void setup()</span>  and <span class="lang:arduino decode:true  crayon-inline " >void loop()</span>. The prefix <span class="lang:arduino decode:true  crayon-inline " >void</span> is how you know that <span class="lang:arduino decode:true  crayon-inline " >setup()</span> and <span class="lang:arduino decode:true  crayon-inline " >loop()</span> are functions (and, as a reminder, a function is something that performs an action [sometimes I like to think of it as a verb]).
<br/>Typically one assumes that a function is capable of accepting an input and producing some output (generally the output is dictated in part by what the input was set to). For a more concrete example, let's start by writing a function that will allow us to print two numbers to the Serial monitor.


<pre class="lang:arduino decode:true " >/*
 * function demo
 * Casey Anderson, SU2015
 *
 * prints two numbers passed to a function as parameters
 *
 * message format: number \t number \nl
 */

void setup(){

    Serial.begin(9600);

}

void loop(){
    function(1000, 13);
    delay(3);
}

void function(int interval, int pin){

    Serial.print(interval);
    Serial.print("\t");
    Serial.println(pin);

}

</pre>
<br/>Arduino expects to see both  <span class="lang:arduino decode:true  crayon-inline " >void setup()</span> and <span class="lang:arduino decode:true  crayon-inline " >void loop()</span>, but it does not necessarily expect to see a user-defined function unless we tell it about these additional functions. In order to do so, we would <strong>declare</strong> the function at the bottom of the window (take a look to see where I have the definition of <span class="lang:arduino decode:true  crayon-inline " >void function()</span>).
<br/>We declare the function by prepending it with <span class="lang:arduino decode:true  crayon-inline " >void</span> and then define the function by telling Arduino what it can do. Once we have done that, we can then use the function in <span class="lang:arduino decode:true  crayon-inline " >void loop()</span>. If we do not define the function, Arduino will not know what we mean by, in this case, <span class="lang:arduino decode:true  crayon-inline " >function(1000, 13)</span> and will throw an error to communicate that to you.
<br/>Let's make this a little more interesting by using a random number generator to change the numbers in our Serial message on every pass through the loop.


<pre class="lang:arduino decode:true " >/*
 * function demo
 * Casey Anderson, SU2015
 *
 * prints two numbers passed to a function as parameters
 *
 * message format: number \t number \nl
 */

int n1, n2;

void setup(){

    Serial.begin(9600);

}

void loop(){

    n1 = random( 200, 2000);
    n2 = random(200, 2000);

    function(n1, n2);

    delay(3);
}

void function(int interval, int pin){

    Serial.print(interval);
    Serial.print("\t");
    Serial.println(pin);

}
</pre>
<br/>Woo, random numbers separated by a tab! Let's do something less arbitrary and go back to the timer example up at the top.
<br/>Here is one solution to converting our  <span class="lang:arduino decode:true  crayon-inline " >millis()</span>  timer into a reusable function. It uses a totally different design pattern than what we have been looking at in Arduino, so take some time to try to figure it out/play with it. How does this work?
<br/>Flasher class, and this example, sourced from Adafruit.

<pre class="lang:arduino decode:true " >class Flasher
{
	// Class Member Variables
	// These are initialized at startup
	int ledPin;      // the number of the LED pin
	long OnTime;     // milliseconds of on-time
	long OffTime;    // milliseconds of off-time

	// These maintain the current state
	int ledState;             		// ledState used to set the LED
	unsigned long previousMillis;  	// will store last time LED was updated

  // Constructor - creates a Flasher
  // and initializes the member variables and state
  public:
  Flasher(int pin, long on, long off)
  {
	ledPin = pin;
	pinMode(ledPin, OUTPUT);

	OnTime = on;
	OffTime = off;

	ledState = LOW;
	previousMillis = 0;
  }

  void Update()
  {
    // check to see if it's time to change the state of the LED
    unsigned long currentMillis = millis();

    if((ledState == HIGH) &amp;&amp; (currentMillis - previousMillis >= OnTime))
    {
    	ledState = LOW;  // Turn it off
      previousMillis = currentMillis;  // Remember the time
      digitalWrite(ledPin, ledState);  // Update the actual LED
    }
    else if ((ledState == LOW) &amp;&amp; (currentMillis - previousMillis >= OffTime))
    {
      ledState = HIGH;  // turn it on
      previousMillis = currentMillis;   // Remember the time
      digitalWrite(ledPin, ledState);	  // Update the actual LED
    }
  }
};


Flasher led1(12, 100, 400);
Flasher led2(13, 350, 350);

void setup()
{
}

void loop()
{
	led1.Update();
	led2.Update();
}
</pre>
